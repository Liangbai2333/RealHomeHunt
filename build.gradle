buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'org.cadixdev.licenser' version '0.6.1'
}

apply plugin: 'net.minecraftforge.gradle'

group = 'site.liangbai'
version = '1.2.3-SNAPSHOT'

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
}

repositories {
    maven { url "https://repo2s.ptms.ink/repository/maven-releases/" }
    mavenCentral()

    maven {
        name 'cursemaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

configurations {
    implementation.extendsFrom shaded
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${minecraft_version}-${forge_version}"

    implementation 'ink.ptms.core:v11604:11604:all'

    implementation fg.deobf("curse.maven:crafting_dead-${crafting_dead_project_id}:${crafting_dead_file_id}")

    // https://mvnrepository.com/artifact/com.dieselpoint/norm
    shaded group: 'com.dieselpoint', name: 'norm', version: '1.0.4'

    implementation fileTree(dir: 'libs', includes: ['*.jar'])
    annotationProcessor fileTree(dir: 'libs', includes: ['*.jar'])
}

jar {
    manifest {
        attributes([
                "Implementation-Project": project.name,
                "Implementation-Version": project.version,
                "Implementation-Author": "Liangbai",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Implementation-Builder": System.properties.getProperty('user.name'),
                "Implementation-Os": System.properties.getProperty('os.name') + System.properties.getProperty('os.arch') + System.properties.getProperty('os.version')
        ])
    }

    from {
        configurations.shaded.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

jar.finalizedBy('reobfJar')

license {
    include '**/*.java'

    header = rootProject.file('HEADER.txt')

    ext {
        organization = 'Liangbai'
        year = 2021
    }
}