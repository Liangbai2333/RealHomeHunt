buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.cadixdev.licenser' version '0.6.1'
    id "io.izzel.taboolib" version '1.34'
    id "org.jetbrains.kotlin.jvm" version '1.6.10'
}

apply plugin: 'net.minecraftforge.gradle'

group = 'site.liangbai'
version = '1.4.5'

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
}

taboolib {
    description {
        it.contributors {
            it.name("Liangbai").description("The plugin's author.")
        }

        it.dependencies {
            it.name("Residence").optional(true)
            it.name("Multiverse-Core").optional(true)
        }
    }
    install("common")
    install("common-5")
    install("platform-bukkit")
    install("module-chat")
    install("module-configuration")
    install("module-database")
    install("module-effect")
    install("module-lang")
    install("module-nms")
    install("module-nms-util")
    classifier = null
    version = "6.0.7-50"
}

repositories {
    mavenCentral()
    // For test
    mavenLocal()
    maven { url 'https://repo1.maven.org/maven2' }
    maven { url 'https://repo.tabooproject.org/repository/releases' }
    maven { url 'https://maven.izzel.io/releases' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://maven.izzel.io/releases' }
    maven { url 'https://maven.parchmentmc.org' }

    maven {
        name 'cursemaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${minecraft_version}-${forge_version}"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation("ink.ptms.core:v11802:11802-minimize:universal")
    implementation 'io.izzel.arclight:arclight-api:1.2.6'
    implementation fg.deobf("curse.maven:crafting_dead-${crafting_dead_project_id}:${crafting_dead_file_id}")
    implementation group: 'org.jetbrains', name: 'annotations', version: '21.0.1'
    implementation fileTree(dir: 'libs', includes: ['*.jar'])
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes([
                "Implementation-Project": project.name,
                "Implementation-Version": project.version,
                "Implementation-Author": "Liangbai",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Implementation-Builder": System.properties.getProperty('user.name'),
                "Implementation-Os": System.properties.getProperty('os.name') + System.properties.getProperty('os.arch') + System.properties.getProperty('os.version')
        ])
    }

    from("LICENSE") {
        rename { "${it}_${rootProject.name}" }
    }
}

jar.finalizedBy('reobfJar')

license {
    include 'site/liangbai/**/*.java'
    include 'site/liangbai/**/*.kt'

    header = rootProject.file('HEADER.txt')

    ext {
        organization = 'Liangbai'
        year = 2022
    }
}