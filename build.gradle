buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.cadixdev.licenser' version '0.6.1'
    id "io.izzel.taboolib" version '1.16'
    id "org.jetbrains.kotlin.jvm" version '1.5.10'
}

apply plugin: 'net.minecraftforge.gradle'

group = 'site.liangbai'
version = '1.4.0'

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
}

taboolib {
    description {
        it.contributors {
            it.name("Liangbai")
        }

        it.dependencies {
            it.name("Multiverse-Core").optional(true)
        }
    }
    install("common")
    install("platform-bukkit")
    classifier = null
    version = "6.0.0-pre46"
}

repositories {
    maven {
        name 'cursemaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

configurations {
    implementation.extendsFrom shaded
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${minecraft_version}-${forge_version}"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'ink.ptms.core:v11604:11604:all'
    implementation fg.deobf("curse.maven:crafting_dead-${crafting_dead_project_id}:${crafting_dead_file_id}")
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '21.0.1'
    // https://mvnrepository.com/artifact/com.dieselpoint/norm
    shaded group: 'com.dieselpoint', name: 'norm', version: '1.0.4'
    shaded fileTree(dir: 'shaded', includes: ['*.jar'])
    implementation fileTree(dir: 'libs', includes: ['*.jar'])
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                name: rootProject.name,
                version: project.version,
                main: project.group + ".realhomehunt.RealHomeHuntPlugin"
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes([
                "Implementation-Project": project.name,
                "Implementation-Version": project.version,
                "Implementation-Author": "Liangbai",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Implementation-Builder": System.properties.getProperty('user.name'),
                "Implementation-Os": System.properties.getProperty('os.name') + System.properties.getProperty('os.arch') + System.properties.getProperty('os.version')
        ])
    }

    from {
        configurations.shaded.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    from("LICENSE") {
        rename { "${it}_${rootProject.name}" }
    }
}

jar.finalizedBy('reobfJar')

license {
    include 'site/liangbai/**/*.java'
    include 'site/liangbai/**/*.kt'

    header = rootProject.file('HEADER.txt')

    ext {
        organization = 'Liangbai'
        year = 2021
    }
}